
program
    = {top_level}
top_level
    = MODULE ID
    | IMPORT ID [AS ID]
    | decl
decl
    = LET ID '=' expr
    | FUN ID param '=' expr
    | expr
expr
    = FN param_list '->' expr
    | IF expr THEN expr ELSE expr
    | MATCH expr '{' match_list '}'
    | '{' {decl} '}'
    | logical_expr
param_list
    = '()' | param {param}
param
    = '_' | ID
match_list
    = pattern '->' expr {'|' pattern '->' expr}
pattern
    = or_pattern [AS ID]
or_pattern
    = or_pattern '|' cons_pattern
    | cons_pattern
cons_pattern
    = a_pattern [':' cons_pattern]
a_pattern
    | '_'
    | Literal
    | '(' list_pattern ')'
    | '[' list_pattern ']'
    | '[]'
    | ID
list_pattern
    = pattern
    | pattern ',' list_pattern


logical_expr
    = equal_expr {logical_op equal_expr}
equal_expr
    = cons_expr [equal_op cons_expr]
cons_expr
    = add_expr {':' add_expr}
add_expr
    = mul_expr {add_op mul_expr}
mul_expr
    = apply_expr {mul_op apply_expr}
apply_expr
    = unary_expr {simple_expr}
unary_expr
    = [unary_op] simple_expr
simple_expr
    = ID ['.' ID]
    | BOOL_LIT | INT_LIT | CHAR_LIT | STRING_LIT 
    | '()' | '(' expr {',' expr} ')'
    | '[' [expr {',' expr}] ']'
logical_op = '||' | '&&'
equal_op = '==' | '!=' | '<' | '<=' | '>' | '>='
add_op = '+' | '-'
mul_op = '*' | '/' | '%'
unary_op = '!' | '-'

