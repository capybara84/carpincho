
program
    = {top_level}
top_level
    = MODULE C_ID
    | IMPORT C_ID [AS C_ID]
    | type_def
    | decl
type_def
    = TYPE C_ID '=' type
type
    = UNIT | BOOL | INT | CHAR | FLOAT | STRING
    | C_ID
    | TVAR
    | '(' type ')'
    | '(' type {',' type} ')'
    | '[' type ']'
    | type '->' type

decl
    = LET ID '=' expr
    | FUN ID param '=' expr
    | expr
expr
    = FN param_list '->' expr
    | IF expr THEN expr ELSE expr
    | MATCH expr '{' match_list '}'
    | '{' {decl} '}'
    | logical_expr
param_list
    = '()' | param {param}
param
    = '_' | ID
match_list
    = ['|'] pattern '->' expr {'|' pattern '->' expr}
pattern
    | or_pattern [AS ID]
or_pattern
    = cons_pattern {'|' cons_pattern}
cons_pattern
    = a_pattern {':' a_pattern}
a_pattern
    | '[]'
    | '_'
    | ID
    | BOOL_LIT | INT_LIT | CHAR_LIT | STRING_LIT 
    | '(' pattern_list ')'
    | '[' pattern_list ']'
pattern_list
    = pattern
    | pattern ',' pattern_list

logical_expr
    = equal_expr {logical_op equal_expr}
equal_expr
    = cons_expr [equal_op cons_expr]
cons_expr
    = add_expr {':' add_expr}
add_expr
    = mul_expr {add_op mul_expr}
mul_expr
    = apply_expr {mul_op apply_expr}
apply_expr
    = unary_expr {simple_expr}
unary_expr
    = [unary_op] simple_expr
simple_expr
    = [C_ID '.'] ID
    | BOOL_LIT | INT_LIT | CHAR_LIT | STRING_LIT 
    | '()' | '(' expr {',' expr} ')'
    | '[' [expr {',' expr}] ']'
logical_op = '||' | '&&'
equal_op = '==' | '!=' | '<' | '<=' | '>' | '>='
add_op = '+' | '-'
mul_op = '*' | '/' | '%'
unary_op = '!' | '-'


-----

program
    = {top_level}
top_level
    = MODULE C_ID
    | IMPORT C_ID [AS C_ID]
    | type_def
    | decl
type_def
    = TYPE [ TVAR | '(' TVAR {',' TVAR} ')' ] C_ID '=' type
type
    = UNIT | BOOL | INT | CHAR | FLOAT | STRING
    | C_ID
    | TVAR
    | type C_ID
    | type '*'
    | '(' type ')'
    | '(' type {',' type} ')'
    | '[' type ']'
    | type '->' type
    | '{' {struct_element} '}'
    | variant
struct_element
    = ID {','ID} ':' type ';'
variant
    = C_ID [type] {'|' variant}

decl
    = LET ID '=' expr
    | FUN ID param '=' expr
    | expr
expr
    = FN param_list '->' expr
    | IF expr THEN expr ELSE expr
    | MATCH expr '{' match_list '}'
    | '{' {decl} '}'
    | logical_expr
param_list
    = '()' | param {param}
param
    = '_' | ID
match_list
    = ['|'] pattern '->' expr {'|' pattern '->' expr}
pattern
    | or_pattern [AS ID]
or_pattern
    = cons_pattern {'|' cons_pattern}
cons_pattern
    = a_pattern {':' a_pattern}
a_pattern
    | '[]'
    | '_'
    | ID
    | C_ID [pattern]
    | BOOL_LIT | INT_LIT | CHAR_LIT | STRING_LIT 
    | '(' pattern_list ')'
    | '[' pattern_list ']'
pattern_list
    = pattern
    | pattern ',' pattern_list

logical_expr
    = equal_expr {logical_op equal_expr}
equal_expr
    = cons_expr [equal_op cons_expr]
cons_expr
    = add_expr {':' add_expr}
add_expr
    = mul_expr {add_op mul_expr}
mul_expr
    = apply_expr {mul_op apply_expr}
apply_expr
    = unary_expr {simple_expr}
unary_expr
    = [unary_op] simple_expr
simple_expr
    = C_ID '.' ID
    | C_ID [expr]
    | C_ID '{' expr {',' expr} '}'
    | ID {'.' ID}
    | BOOL_LIT | INT_LIT | CHAR_LIT | STRING_LIT 
    | '()' | '(' expr {',' expr} ')'
    | '[' [expr {',' expr}] ']'
logical_op = '||' | '&&'
equal_op = '==' | '!=' | '<' | '<=' | '>' | '>='
add_op = '+' | '-'
mul_op = '*' | '/' | '%'
unary_op = '!' | '-'

